#include 'totvs.ch'
#include 'tlpp-core.th'

namespace custom.api.solfacil.faturamento

/*/{Protheus.doc} U_GETPEDIDOS
Programa para uso como API.
Objetivo: Consulta dados de pedidos de vendas para retorno via API rest.

Autenticação: Para acesso a essa API, será necessário enviar dados de autenticação por meio do header da requisição. 
Ela pode ser do tipo basic, nesse caso será enviado username:password, nesse formato, convertido para base64. 
Podendo também ser do tipo Bearer. Nesse caso será enviado um token JWT obtido por meio de requisição do tipo POST 
no endpoint /api/oauth2/v1/token?grant_type=password&password=senha&username=usuario. O retorno desse endpoint será um 
conjunto de informações, sendo:
- access_token, que deve ser enviado no header da requisição
- refresh_token, usado para obter um novo token quando necessário
- token_type, Bearer
- expires_in, que indica a quantidade de segundos para expiraçaõ do token. 3600 segundos ou 1h.
Nenhuma interação via código ADVPL/TLPP será necessária para tratamento de autenticação. O próprio framework tratará isso.

Ainda no header da requisição deve ser enviada a chave TenantId, cujo conteudo deve ser grupo de empresas e filial, separados por virgula. 
Isso é necessário para o correto processo de carregamento dos dados do ambiente.

Idenpotência: Em resumo, é um principio que garante que o estado do sistema não deve ser alterado indevidamente. 
No caso do método GET, como é apenas consulta, ele é naturalmente idenpotente. 
Para demais metodos, pode ser necessário via programação, os mecanismos necessários para evitar que uma operação altere o estado do sistema mais de uma vez.

Segurança já é em parte garantida pela autenticação. Mas é complementada pelo uso de criptografica SSL/HTTPS. Isso é feito no processo de configuração e disponibilização 
do serviço REST, e por tratamentos adequados no código fonte que visem impedir ataques do tipo SQL Injection.

A resiliencia é a capacidade de se recuperar de falhas. Na construção do código advpl, isso pode ser feito usando recursos de controle de transações (Begin Transaction) e 
controle de sequência (try catch e begin sequence). Outros mecanismos devem ser implementados na camada de infraestrutura que suporte o serviço.

Documentação: A API desenvolvida nesse código possui uma função construída no final do arquivo para geração de arquivo json, no formato openApi, que pode ser importado
no editor Swagger para geração de documentação. Exemplos do arquivo json, arquivo yml e um cliente gerado pelo Swagger, com exemplos de consumo da api em diferentes linguagens,
constam no repositório git público: https://github.com/klauswolfgram/solfacil na branche identificada como API.

@type user function
@author Klaus Wolfgram
@since 20/02/2025
@version 1.0

custom.api.solfacil.faturamento.U_GETPEDIDOS
/*/

@get(endpoint='/api/solfacil/faturamento/pedidos/:filial/:pedido',description="[U_GETPEDIDOS_DOC]")

Function U_GETPEDIDOS

    Local lret              as Logical
    Local lRPC              as Logical
    Local err               as Object
    Local cTxtErr           as Character
    Local cFilPed           as Character
    Local cNumPed           as Character
    Local jItemPed          as Json
    Local jPathPar          as Json
    Local jResp             as Json

    try

        lret                := .T.
        jPathPar            := oRest:getPathparametersRequest()    

        IF type('cEmpAnt') <> 'C'
            rpcSetEnv('99','01')
            lRPC            := .T.
        EndIF    

        err                 := errorClass():new()

        IF !jPathPar:hasProperty('filial')
            cTxtErr         := 'Filial nao informada na requisicao'
        EndIF

        IF !jPathPar:hasProperty('pedido')
            cTxtErr         := IF(empty(cTxtErr),"","/") + "Numero do pedido nao informado"
        EndIF   

        IF !empty(cTxtErr)
            err:description := cTxtErr
            break(err)
        EndIF     

        SC5->(dbSetOrder(1),dbSeek(cFilPed+cNumPed))

        IF !SC5->(found())
            err:description := 'Pedido nao encontrado'
            break(err)
        EndIF

        SC6->(dbSetOrder(1),dbSeek(SC5->(C5_FILIAL+C5_NUM)))

        IF !SC6->(found())
            err:description := 'Nenhum item encontrado para o pedido'
            break(err)
        EndIF
        
        cEmissao            := SC5->(dtoc(C5_EMISSAO))
        cNomeCli            := SC5->(if(C5_TIPO $ 'D/B',;
        posicione('SA2',1,xFilial('SA2',C5_FILIAL)+C5_CLIENTE+C5_LOJA,'alltrim(A2_NOME)'),;
        posicione('SA1',1,xFilial('SA1',C5_FILIAL)+C5_CLIENTE+C5_LOJA,'alltrim(A1_NOME)')))

        jResp               := jsonObject():new()
        jResp['filial' ]    := SC5->C5_FILIAL
        jResp['numero' ]    := SC5->C5_NUM
        jResp['tipo'   ]    := SC5->C5_TIPO
        jResp['codigo' ]    := SC5->C5_CLIENTE
        jResp['loja'   ]    := SC5->C5_LOJACLI
        jResp['nome'   ]    := cNomeCli
        jResp['emissao']    := cEmissao
        jResp['nota'   ]    := SC5->C5_NOTA
        jResp['serie'  ]    := SC5->C5_SERIE
        jResp['itens'  ]    := array(0)

        While SC6->(C6_FILIAL+C6_NUM) == SC5->(C5_FILIAL+C5_NUM) .and. !SC6->(eof())
            
            jItemPed                           := jsonObject():new()
            jItemPed['item'                 ]  := SC6->C6_ITEM
            jItemPed['produto'              ]  := SC6->C6_PRODUTO
            jItemPed['descricao'            ]  := posicione('SB1',1,xFilial('SB1',SC5->C5_FILIAL)+SC6->C6_PRODUTO,'alltrim(B1_DESC)')
            jItemPed['quantidade'           ]  := SC6->C6_QTDVEN
            jItemPed['saldoentreg'          ]  := SC6->(C6_QTDVEN - C6_QTDENT)
            jItemPed['quantidade_liberada'  ]  := fnGetQtdLibFat()
            jItemPed['nota'                 ]  := SC6->C6_NOTA
            jItemPed['serie'                ]  := SC6->c6_SERIE

            aadd(jResp['itens'],jItemPed)

        Enddo

        oRest:setResponse(jResp:toJson())

    catch e

        IF empty(e:description)
            cTxtErr         := 'Ocorreu um erro indefinido na execucao da requisicao'
        Else    
            cTxtErr         := e:description
        EndIF   
    
        jResp               := jsonObject():new()
        jResp['response']   := cTxtErr

        oRest:setFault(jResp:toJson())

    finally

        IF lRPC
            rpcClearEnv()
        EndIF        

    endtry

Return lret

/*/{Protheus.doc} fnGetQtdLibFat
    Retorna a quantidade liberada para faturamento referente ao item do pedido de vendas posicionado.
    @type  Static Function
    /*/
Static Function fnGetQtdLibFat() as Numeric

    Local nQtdLib   as Numeric
    Local cAliasSQL as Character

    cAliasSQL       := getNextAlias()

    BeginSQL alias cAliasSQL
        SELECT ISNULL(SUM(C9_QTDLIB),0) C9_QTDLIB
        FROM %table:SC9% SC9
        WHERE SC9.%notdel%
        AND C9_FILIAL = %exp:SC6->C6_FILIAL%
        AND C9_PEDIDO = %exp:SC6->C6_NUM%
        AND C9_ITEM   = %exp:SC6->C6_ITEM%
        AND C9_NFISCAL= ''
    EndSQL

    (cAliasSQL)->(dbEval({|| nQtdLib := C9_QTDLIB}),dbCloseArea())
    
Return nQtdLib

/*/{Protheus.doc} GETPEDIDOS_DOC
Programa para retornar a documentação no formato openAPI referente a funcao GetPedidos
@type user function
@author Klaus Wolfgram
@since 19/02/2025
@version 1.0

custom.api.solfacil.faturamento.U_GETPEDIDOS_DOC
/*/
User Function GETPEDIDOS_DOC() as Character

    Local jDoc          as Json
    Local jInfo         as Json
    Local jParam        as json
    Local jServer       as Json
    Local jOperation    as json
    Local jGet          as json
    Local jPath         as Json
    Local jSchema       as Json
    Local cJsonDoc      as Character
    Local cLib          as Character

    jDoc                      := jsonObject():new()
    jDoc['openapi'          ] := '3.0.0'

    jInfo                     := jsonObject():new()
    jInfo['title'           ] := 'Consulta status do pedido de vendas'
    jInfo['version'         ] := '1.0'
    jInfo['description'     ] := 'Permite a consulta dos dados de liberacao e faturamento do pedido de vendas'
    jInfo['description'     ] += CRLF + 'Para o correto funcionamento eh necessário informar o seguintes parametros:'
    jInfo['description'     ] += CRLF + 'header: Authorization - Dados de autenticacao Basic ou Bearer'
    jInfo['description'     ] += CRLF + 'header: TenantId - Codigo da empresa,filial para preparacao do ambiente'
    jInfo['description'     ] += CRLF + 'Path: filial - Filial indicada no pedido de vendas'
    jInfo['description'     ] += CRLF + 'Path: pedido - Numero do pedido a ser consultado'
    
    jDoc['info'             ] := jInfo
    jDoc['servers'          ] := array(0)

    jServer                   := jsonObject():new()
    jServer['url'           ] := 'https://protheus.solfacil.com.br'
    jServer['description'   ] := 'Servidor de producao'
    aadd(jDoc['servers'],jServer)

    jServer                   := jsonObject():new()
    jServer['url'           ] := 'https://hml.protheus.solfacil.com.br'
    jServer['description'   ] := 'Servidor de homologacao'
    aadd(jDoc['servers'],jServer)  
    
    jGet                      := jsonObject():new()
    jGet['summary'         ]  := 'Obtem detalhes do pedido de vendas'
    jGet['description'     ]  := 'Retorna os dados de status do pedido de vendas de acordo com os dados informados nos parametros'
    jGet['parameters'      ]  := array(0)

    jParam                    := jsonObject():new()
    jParam['name'           ] := 'Authorization'
    jParam['description'    ] := 'Dados de autenticacao para api rest. Pode ser basic ou bearer(Token JWT). Consulte a documentacao disponivel em: https://tdn.totvs.com/pages/viewpage.action?pageId=578841720'
    jParam['in'             ] := 'header'
    jParam['required'       ] := .T.

    jSchema                   := jsonObject():new()
    jSchema['type'          ] := 'string'
    
    jParam['schema'         ] := jSchema
    aadd(jGet['parameters'],jParam)

    jParam                    := jsonObject():new()
    jParam['name'           ] := 'tenantId'
    jParam['description'    ] := 'Codigo de empresa e filial, no formato EE,FFFF para preparacao do ambiente.'
    jParam['in'             ] := 'header'
    jParam['required'       ] := .T.

    jSchema                   := jsonObject():new()
    jSchema['type'          ] := 'string'
    
    jParam['schema'         ] := jSchema
    aadd(jGet['parameters'],jParam) 

    jParam                    := jsonObject():new()
    jParam['name'           ] := 'filial'
    jParam['description'    ] := 'filial no formato FFFF indicada no pedido de vendas.'
    jParam['in'             ] := 'path'
    jParam['required'       ] := .T.

    jSchema                   := jsonObject():new()
    jSchema['type'          ] := 'string'
    
    jParam['schema'         ] := jSchema
    aadd(jGet['parameters'],jParam) 

    jParam                    := jsonObject():new()
    jParam['name'           ] := 'pedido'
    jParam['description'    ] := 'Numero no formato NNNNNN indicado no pedido de vendas.'
    jParam['in'             ] := 'path'
    jParam['required'       ] := .T.

    jSchema                   := jsonObject():new()
    jSchema['type'          ] := 'string'
    
    jParam['schema'         ] := jSchema
    aadd(jGet['parameters'],jParam)

    jResp                     := jsonObject():new()

    j200                      := jsonObject():new()
    j200['description'       ]:= 'Dados de status dos itens dos pedidos'
    jResp['200']              := j200

    jGet['responses'         ]:= jResp

    jOperation                := jsonObject():new()
    jOperation['get'          ] := jGet
    
    jPath                     := jsonObject():new()
    jPath['/api/solfacil/faturamento/pedidos/{filial}/{pedido}'] := jOperation    

    jDoc['paths']             := jPath

    cJsonDoc                  := jDoc:toJson()    

    IF getRemoteType(@cLib) >= 0
        
        IF "LINUX" $ cLib
            cTarget := '/tmp/doc.api.solfacil.faturamento.pedidos.json'
            memowrite(cTarget,cJsonDoc)  
        Else
            cTarget := 'C:\Temp\doc.api.solfacil.faturamento.pedidos.json'
            memowrite(cTarget,cJsonDoc)
        EndIF

        fwAlertSuccess('Arquivo json de documentacao gerado em: ' + cTarget)

    EndIF

    cTarget := '\data\doc.api.solfacil.faturamento.pedidos.json'

    IF !file(cTarget)
        memowrite(cTarget,cJsonDoc)   
    EndIF      

Return cJsonDoc
